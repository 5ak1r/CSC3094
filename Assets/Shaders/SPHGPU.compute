// spatial hashing optimisation kernels
#pragma kernel HashParticles
#pragma kernel BitonicSort
#pragma kernel FillLookupTable

// simulation kernels
#pragma kernel CalculateProperties
#pragma kernel CalculateForces
#pragma kernel MoveParticles

struct ParticleGPU
{
    float pressure;
    float density;

    float3 forces;
    float3 velocity;
    float3 position;
};

// general settings
uint particleCount;

float3 boxDimensions;

float epsilon;
float deltaTime;

// fluid settings
float particleMass;
float restDensity;
float gasConstant;
float viscosity;
float surfaceTensionThreshold;
float tensionCoefficient;

// particle settings
float smoothingRadius;
float smoothingRadius2;
float smoothingRadius4;

// kernel settings
float polyMult; // 315 / (64*pi*smoothingRadius3)
float polyGradMult; // -945 / (32*pi*smoothingRadius5)
float polyLapMult; // -945 / (32*pi*smoothingRadius9)
float spikyGradMult; // -45 / (pi*smoothingRadius3)
float viscLapMult; // 45 / (pi*smoothingRadius5)

// spatial hash settings
int block;
int dim;

// collision object settings
float3 collisionPosition;
float collisionRadius;
float collisionForce;

// buffers
RWStructuredBuffer<ParticleGPU> _particles;
RWStructuredBuffer<uint> _particleIndices;
RWStructuredBuffer<uint> _cellIndices;
RWStructuredBuffer<uint> _lookupTable;

// spatial hashing

inline int3 FindCell(in float3 position)
{
    return int3(floor(position / smoothingRadius));
}

inline uint FindHash(int3 cell)
{
    return (
        cell.x * 73856093 ^
        cell.y * 19349663 ^
        cell.z * 83492791
    ) % particleCount;
}

[numthreads(256,1,1)]
void HashParticles(int3 id: SV_DISPATCHTHREADID)
{
    _lookupTable[id.x] = 0xFFFFFFFF;

    uint particleIndex = _particleIndices[id.x];
    _cellIndices[particleIndex] = FindHash(FindCell(_particles[particleIndex].position));
}

[numthreads(256,1,1)]
void BitonicSort(int3 id: SV_DISPATCHTHREADID)
{
    uint i = id.x + id.y * 256 * 1024;
    uint j = i ^ block;

    if(j <= i || i >= particleCount || j >= particleCount) return;

    uint keyI = _particleIndices[i];
    uint keyJ = _particleIndices[j];

    float valueI = _cellIndices[keyI];
    float valueJ = _cellIndices[keyJ];

    float diff = (valueI - valueJ) * ((i & dim) == 0 ? 1 : -1);
    
    if (diff > 0)
    {
        _particleIndices[i] = keyJ;
        _particleIndices[j] = keyI;
    }
}

[numthreads(256,1,1)]
void FillLookupTable(uint3 id: SV_DISPATCHTHREADID)
{
    uint particleIndex = _particleIndices[id.x];
    uint cellIndex = _cellIndices[particleIndex];

    InterlockedMin(_lookupTable[cellIndex], id.x);
}

// simulation

// kernels
// https://rlguy.com/sphfluidsim/
inline float Poly6(in float distance2)
{
    float x = 1 - distance2 / smoothingRadius2;
    return polyMult * x * x * x;
}

// https://courses.grainger.illinois.edu/cs418/fa2024/text/sph.html
inline float3 Poly6Gradient(in float distance2, in float3 direction)
{
    float x = 1 - distance2 / smoothingRadius2;
    return polyGradMult * x * x * direction;
}

inline float Poly6Laplacian(in float distance2)
{
    float x = 3 * smoothingRadius4 - 10 * smoothingRadius2 * distance2 + 7 * distance2 * distance2;
    return polyLapMult * x;
}

inline float3 SpikyGradient(in float distance, in float3 direction)
{
    float x = 1 - distance / smoothingRadius;
    return spikyGradMult * x * x * direction;
}

// https://matthias-research.github.io/pages/publications/sca03.pdf
inline float ViscosityLaplacian(in float distance)
{
    float x = 1 - distance / smoothingRadius;
    return viscLapMult * x;
}

[numthreads(256,1,1)]
void CalculateProperties(int3 id: SV_DISPATCHTHREADID)
{
    float density = 0;

    uint particleIndex = _particleIndices[id.x];
    int3 cellIndex = FindCell(_particles[particleIndex].position);

    for (int x = -1; x <= 1; x++)
    {
        for (int y = -1; y <= 1; y++)
        {
            for (int z = -1; z <= 1; z++)
            {
                int3 neighbour = cellIndex + int3(x, y, z);
                uint neighbourHash = FindHash(neighbour);
                uint start = _lookupTable[neighbourHash];

                while(start != 0xFFFFFFFF && start < particleCount)
                {
                    uint otherIndex = _particleIndices[start];
                    
                    // if not equal, not in same cell
                    if (_cellIndices[otherIndex] != neighbourHash)
                        break;

                    ParticleGPU other = _particles[otherIndex];

                    float3 offset = _particles[particleIndex].position - other.position;
                    float distance2 = dot(offset, offset);

                    if (distance2 < smoothingRadius2)
                        density += Poly6(distance2) * particleMass;

                    start++;
                }
            }
        }
    }

    _particles[particleIndex].density = density + epsilon;
    _particles[particleIndex].pressure = max(0, gasConstant * (_particles[particleIndex].density - restDensity)); // clamp pressure for simulation purposes
}

[numthreads(256,1,1)]
void CalculateForces(int3 id: SV_DISPATCHTHREADID)
{
    float3 gravityForce = float3(0, -9.81, 0) * particleMass;
    float3 pressureForce = float3(0,0,0);
    float3 viscosityForce = float3(0,0,0);

    float3 surfaceTensionForce = float3(0,0,0);
    
    float3 colorFieldGrad = float3(0,0,0);
    float colorFieldLap = 0;

    uint particleIndex = _particleIndices[id.x];
    int3 cellIndex = FindCell(_particles[particleIndex].position);

    for (int x = -1; x <= 1; x++)
    {
        for (int y = -1; y <= 1; y++)
        {
            for (int z = -1; z <= 1; z++)
            {
                int3 neighbour = cellIndex + int3(x, y, z);
                uint neighbourHash = FindHash(neighbour);
                uint start = _lookupTable[neighbourHash];

                while(start != 0xFFFFFFFF && start < particleCount)
                {
                    uint otherIndex = _particleIndices[start];
                    
                    // if not equal, not in same cell
                    if (_cellIndices[otherIndex] != neighbourHash)
                        break;

                    ParticleGPU other = _particles[otherIndex];

                    if (particleIndex == otherIndex)
                    {   
                        start++; 
                        continue;
                    }

                    float3 offset = _particles[particleIndex].position - other.position;
                    float distance2 = dot(offset, offset);

                    if (distance2 < smoothingRadius2)
                    {   
                        float distance = sqrt(distance2);
                        float3 dir = normalize(offset);
                        pressureForce -= particleMass * (_particles[particleIndex].pressure + other.pressure) / (2 * other.density) * SpikyGradient(distance, dir);
                        viscosityForce += particleMass * (other.velocity - _particles[particleIndex].velocity) / other.density * ViscosityLaplacian(distance);
                        
                        // color field for surface tension calculations
                        colorFieldGrad += particleMass / other.density * Poly6Gradient(distance2, dir);
                        colorFieldLap += particleMass / other.density * Poly6Laplacian(distance2);
                    }

                    start++;
                }
            }
        }
    }

    //check if surface tension applies here
    float colorFieldGradLength = length(colorFieldGrad);

    if (colorFieldGradLength > surfaceTensionThreshold)
    {
        float curvature = -colorFieldLap / colorFieldGradLength;
        surfaceTensionForce = -tensionCoefficient * curvature * normalize(colorFieldGrad);
    }

    _particles[particleIndex].forces = gravityForce + pressureForce + viscosityForce + surfaceTensionForce;

    // collision external force
    float3 collisionDir = _particles[particleIndex].position - collisionPosition;
    float distance2 = dot(collisionDir, collisionDir);

    if (distance2 < collisionRadius * collisionRadius + smoothingRadius)
    {
        // only square root if needed; expensive operation
        float distance = sqrt(distance2);
        float3 normalizeDir = collisionDir / distance;
        float falloff = 1 - (distance / collisionRadius);
        _particles[particleIndex].forces += normalizeDir * collisionForce * falloff;
    }
}

[numthreads(256,1,1)]
void MoveParticles(int3 id: SV_DISPATCHTHREADID)
{
    const float damping = -0.3;

    float3 vel = _particles[id.x].velocity + deltaTime * _particles[id.x].forces / particleMass; //v = u + at, F = ma
    float3 pos = _particles[id.x].position + vel * deltaTime;

    // box collision resolutions
    if (pos.x - smoothingRadius < 0) 
    {
        pos.x = smoothingRadius;
        vel.x *= damping;
    }
    else if (pos.x + smoothingRadius > boxDimensions.x)
    {
        pos.x = boxDimensions.x - smoothingRadius;
        vel.x *= damping;
    }

    if (pos.y - smoothingRadius < 0)
    {
        pos.y = smoothingRadius;
        vel.y *= damping;
    }
    else if (pos.y + smoothingRadius > boxDimensions.y)
    {
        pos.y = boxDimensions.y - smoothingRadius;
        vel.y *= damping;
    }

    if (vel.y < 0.1) vel.y == 0;

    if (pos.z - smoothingRadius < 0)
    {
        pos.z = smoothingRadius;
        vel.z *= damping;
    }
    else if (pos.z + smoothingRadius > boxDimensions.z)
    {
        pos.z = boxDimensions.z - smoothingRadius;
        vel.z *= damping;
    }

    _particles[id.x].velocity = vel;
    _particles[id.x].position = pos;
}